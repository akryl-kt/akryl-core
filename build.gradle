plugins {
    id 'org.jetbrains.kotlin.js' version '1.3.61'
}

apply plugin: "maven-publish"

group 'io.akryl'
version '0.7.0'

repositories {
    maven { url = "https://dl.bintray.com/kotlin/kotlin-eap" }
    maven { url = "https://dl.bintray.com/kotlin/kotlin-dev" }
    mavenCentral()
    jcenter()
}

kotlin {
    target {
        browser {
            testTask {
                useKarma {
                    useChromeHeadless()
                }
            }
        }

    }

    sourceSets {
        main {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js"

                implementation npm("babel-loader", "8.0.6")
                implementation npm("@babel/core", "7.7.7")
                implementation npm("@babel/preset-env", "7.7.7")
                implementation npm("react", "16.12.0")
                implementation npm("react-test-renderer", "16.12.0")
            }
        }
    }
}

kotlinNpmInstall {
    doLast {
        exec {
            workingDir "$projectDir/build/js"
            executable "npm"
            args "link", "babel-plugin-akryl"
        }
    }
}

dependencies {
    testImplementation "org.jetbrains.kotlin:kotlin-test-js"
}

compileKotlinJs {
    kotlinOptions {
        freeCompilerArgs += "-Xuse-experimental=kotlin.experimental.ExperimentalTypeInference"
    }
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from "src/main/kotlin"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.akryl'
            artifactId = 'akryl-core'
            version = '0.7.0'

            from components.kotlin
            artifact sourcesJar
        }
    }
}
